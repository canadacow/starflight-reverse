// GLSL Compute Shader
#version 450

#extension GL_GOOGLE_include_directive : enable

#include "uniform.h"

layout(local_size_x = 4, local_size_y = 4) in;

// Equivalent of TextData struct
struct TextData {
    uint character;
    uint xormode;
    uint fontNum;
};

// Equivalent of LineData struct
struct LineData {
    uint x0;
    uint y0;
    uint x1;
    uint y1;
    uint n;
    uint total;
};

struct RunBitData {
    uint tag;
};

// Equivalent of Rotoscope struct
struct Rotoscope {
    uint content;
    uint EGAcolor;
    uint argb;
    int blt_x;
    int blt_y;
    int blt_w;
    int blt_h;
    uint bgColor;
    uint fgColor;
    TextData textData;
    LineData lineData;
    RunBitData runBitData;
    uint navMask;
    uint chromaKey;
};

layout(rgba8, binding = 0) writeonly uniform image2D imgOutput;

layout(std430, binding = 1) buffer RotoBuffer {
    Rotoscope rotoPixels[];
};

layout(binding = 2) uniform sampler2DArray ShipTexture;
layout(binding = 3) uniform sampler2DArray PlanetTextures;

layout(binding = 4) uniform UB {
    UniformBlock uniforms;
} ub;

layout(binding = 5) uniform IconBlock {
    Icon icons[64];
};

#include "tables.h"
#include "noise.h"
#include "distance.h"
#include "hyperspace_stars.h"
#include "starburst.h"
#include "nebula.h"
#include "sun.h"
#include "planet.h"
#include "engines.h"

float LineSegmentDistance(vec2 p, vec2 a, vec2 b) {
    vec2 pa = p - a, ba = b - a;
    float h = clamp(dot(pa, ba) / dot(ba, ba), 0.0, 1.0);
    return length(pa - ba * h);
}

#define GLOW 0.55
#define NUM_PARTICLES 75.0

vec3 particles(vec2 uv, vec3 color, float radius, float offset, float yPosition)
{        
    // Adjust position calculation to use a fixed yPosition for horizontal banding
    vec2 position = vec2(offset, yPosition);
    
    float dist = radius / distance(uv, position);
    return color * pow(dist, 1.0 / GLOW);
}

vec4 DrawNavigationPixel(const Rotoscope roto, vec2 uv, vec2 subUv) {
    vec4 pixel = vec4(0.);

    vec4 fragColor;
    vec2 sub;

    float iTime = ub.uniforms.iTime;

    float scrOffX = float(roto.blt_x) + subUv.x;
    float scrOffY = float(roto.blt_y) + subUv.y;

    vec2 pixelPos = vec2(scrOffX, scrOffY);

    pixelPos -= vec2(80.0, 100.0);

    float subX = scrOffX / float(roto.blt_w);
    float subY = scrOffY / float(roto.blt_h);

    // Split view -- helpful to debug
    #if 0
    if(subY > 0.5)
    {
        return vec4(unpackUnorm4x8(roto.argb)).bgra;
    }
    #endif

    vec2 iResolution = vec2(float(roto.blt_w), float(roto.blt_h));
    vec2 fragCoord = pixelPos + vec2(80.0, 100.0);

    vec2 worldCoords = vec2((ub.uniforms.worldX) / 1000.0, (ub.uniforms.worldY) / 1000.0);
    pixelPos += vec2(ub.uniforms.worldX, ub.uniforms.worldY);

    vec4 starPixel = vec4(0.);
    vec4 sunPixel = vec4(0.);
    vec4 iconPixel = vec4(0.);
    vec4 shipPixel = vec4(0.);
    vec4 planetPixel = vec4(0.);
    float nebulaPixel = ub.uniforms.nebulaBase;

    for(int i = 0; i < 64; i++) {
        Icon icon = icons[i];

        if(icon.isActive == 0)
            continue;

        vec2 iconPos = vec2(float(icon.screenX), float(icon.screenY));

        iconPos *= ub.uniforms.zoomLevel;

        // 27 is RIGHT
        // 28 is UP to the RIGHT
        // 29 is UP
        // 30 is UP to the LEFT
        // 31 is LEFT
        // 32 is DOWN to the LEFT
        // 33 is DOWN
        // 34 is DOWN to the RIGHT
        if(icon.id >= 27 && icon.id <= 34) {
            float distance = ub.uniforms.zoomLevel;
            if(calculateBoundingBoxDistanceIcon(iconPos, pixelPos) < distance) {
                vec2 normUV = calculateNormalizedUVIcon(iconPos, pixelPos, distance * 2.0);

                normUV -= 0.5;
                // Rotate the normUVs to match the icon.id orientation
                float angle = ub.uniforms.heading; // (icon.id - 27) * 45.0;
                float rad = radians(angle);
                float s = sin(rad);
                float c = cos(rad);
                normUV = vec2(normUV.x * c - normUV.y * s, normUV.x * s + normUV.y * c);
                normUV *= 2.0;

                normUV += 0.5;

                vec4 texColor = texture(ShipTexture, vec3(normUV, 0));
                vec4 ourShipPixel = vec4(texColor.rgb * texColor.a, texColor.a);

                if(normUV.x < 0.02 && normUV.y >= 0.30 && normUV.y <= 0.70 && ub.uniforms.thrust > 0.0)
                {
                    vec2 subUv = vec2((normUV.x + 1.0), ((normUV.y - 0.3) * 2.0) + 0.1);
                    vec4 engines;

                    float thrustRadius = 0.15 + (ub.uniforms.thrust * 0.18);

                    main_engines(engines, subUv, iTime, thrustRadius);
                    ourShipPixel += engines;
                }

                 shipPixel = ourShipPixel  + (shipPixel * (1 - ourShipPixel.a));
            }
        } else if(icon.id >= 51 && icon.id <= 91) {
            float basesize = 29.0f * float(icon.id - 50);

            if(calculateDistance(iconPos, pixelPos) < basesize) {
                iconPixel += colortable[icon.clr & 0xf];
            }
        } else if(icon.id >= 35 && icon.id <= 42) {
            float distance = 0.5 * ub.uniforms.zoomLevel;
            if(calculateBoundingBoxDistanceIcon(iconPos, pixelPos) < distance) {
                vec2 normUV = calculateNormalizedUVIcon(iconPos, pixelPos, distance * 2.0);

                normUV -= 0.5;
                // Rotate the normUVs to match the icon.id orientation
                //float angle = (icon.id - 35) * 45.0;
                float rad = icon.objectHeading; //radians(angle);
                float s = sin(rad);
                float c = cos(rad);
                normUV = vec2(normUV.x * c - normUV.y * s, normUV.x * s + normUV.y * c);

                normUV += 0.5;

                vec4 texColor = texture(ShipTexture, vec3(normUV, 1));
                vec4 vessel = vec4(texColor.rgb * texColor.a, texColor.a);

                shipPixel = vessel  + (shipPixel * (1 - vessel.a));
            }
        } else if(icon.id == 43) {
            // Debris
            float distance = 0.5 * ub.uniforms.zoomLevel;
            if(calculateBoundingBoxDistanceIcon(iconPos, pixelPos) < distance) {
                vec2 normUV = calculateNormalizedUVIcon(iconPos, pixelPos, distance * 2.0);

                normUV -= 0.5;
                // Rotate the normUVs to match the icon.id orientation
                //float angle = (icon.id - 35) * 45.0;
                float rad = 0.0;
                float s = sin(rad);
                float c = cos(rad);
                normUV = vec2(normUV.x * c - normUV.y * s, normUV.x * s + normUV.y * c);

                normUV += 0.5;

                vec4 texColor = texture(ShipTexture, vec3(normUV, 2));
                vec4 vessel = vec4(texColor.rgb * texColor.a, texColor.a);

                shipPixel = vessel  + (shipPixel * (1 - vessel.a));
            }
        } else if(icon.id < 50) {
            float distance = 0.5 * ub.uniforms.zoomLevel;
            if(calculateBoundingBoxDistanceIcon(iconPos, pixelPos) < distance) {
                iconPixel += colortable[icon.clr & 0xf];
            }
        } else {
            float distanceI = 2.0;

            switch(icon.id) {
                case 251:
                    {
                        vec2 start = iconPos.xy;
                        vec2 end = vec2(icon.x1, icon.y1);
                        end *= ub.uniforms.zoomLevel;

                        vec4 baseColor = colortable[icon.clr & 0xf];

                        // Set color to a constant electric blue
                        vec3 color = vec3(0.0, 0.3, 1.0);
                        float radius = 0.5; // Radius of particles
                        float yPosition = 0.0; // This positions the band in the middle of the screen vertically

                        // Calculate the distance from the pixel to the line segment to determine if we should draw a particle
                        float lineDist = LineSegmentDistance(pixelPos.xy, start.xy, end.xy);

                        // Only draw particles if the pixel is close enough to the line
                        if(lineDist <= 0.25 * ub.uniforms.zoomLevel) { // Use a fixed distance to determine "closeness"
                            for (float i = 0.0; i < NUM_PARTICLES; i++) {
                                // Calculate the offset along the line for each particle
                                float t = i / NUM_PARTICLES;
                                vec2 linePos = mix(start, end, t); // Linearly interpolate between start and end points based on t
                                float offset = length(linePos - start) / length(end - start); // Normalize offset based on line length

                                // Adjust yPosition based on the line's orientation
                                yPosition = linePos.y;

                                // Draw the particle
                                vec3 particleColor = particles(pixelPos.xy, color, radius, offset, yPosition);
                                iconPixel += vec4(particleColor, 1.0); // Assuming iconPixel is the accumulator for the pixel color
                            }
                        }
                    }
                    break;
                case 252:
                    {
                        distanceI = 0.25 * ub.uniforms.zoomLevel;
                        if(calculateBoundingBoxDistanceIcon(iconPos, pixelPos) < distanceI) {
                            vec2 normUV = calculateNormalizedUVIcon(iconPos, pixelPos, distanceI * 2.0);

                            normUV -= 0.5;
                            // Rotate the normUVs to match the icon.id orientation
                            //float angle = (icon.id - 35) * 45.0;
                            float rad = 0.0;
                            float s = sin(rad);
                            float c = cos(rad);
                            normUV = vec2(normUV.x * c - normUV.y * s, normUV.x * s + normUV.y * c);

                            normUV += 0.5;

                            vec4 texColor = texture(ShipTexture, vec3(normUV, 3));
                            vec4 vessel = vec4(texColor.rgb * texColor.a, texColor.a);

                            shipPixel = vessel  + (shipPixel * (1 - vessel.a));
                        }
                    }
                    break;
                case 254: // Invisible icon (Encounter hit testing?)
                    if(roto.blt_x == icon.screenX && roto.blt_y == icon.screenY) {
                        iconPixel += colortable[icon.clr & 0xf];
                    }
                    break;
                default:
                    break;
            }
        }
    }

    hyperspaceRender(pixel, fragCoord, iTime, iResolution, worldCoords + vec2(1000.0, 1000.0));

    //pixel = vec4(subUv.xy, 0.0, 1.0);

    if(nebulaPixel > 0.0)
    {
        vec4 p;

        nebulaPixel = min(nebulaPixel / 8.0, 1.0);

        vec2 uv=fragCoord.xy/iResolution.xy-.5;
        nebula(p, uv + (worldCoords * ub.uniforms.nebulaMultiplier), iTime);
        pixel += p * nebulaPixel;
    }

    pixel += starPixel;
    pixel = sunPixel  + (pixel * (1 - sunPixel.a));
    pixel = planetPixel + (pixel * (1 - planetPixel.a));
    pixel = iconPixel + (pixel * (1 - iconPixel.a));
    pixel = shipPixel + (pixel * (1 - shipPixel.a));

#if 0
    // Draw grid at whole integer values of pixelPos
    float gridLineWidth = 0.02; // Width of the grid lines
    vec2 gridPos = mod(pixelPos + vec2(0.5, 0.5), ub.uniforms.zoomLevel);
    if(gridPos.x < gridLineWidth || gridPos.y < gridLineWidth) {
        // Adjust pixel color to draw the grid
        pixel.rgb = vec3(0.0, 1.0, 0.0); // Change pixel color to green for grid lines
    }
#endif
    return pixel;
}

vec4 DrawRadar(vec2 subUv) {
    vec4 radarPixel = vec4(0.0,0.1,0.0,0.0);
    ivec2 radarSize = ivec2(69, 90);
    const int kernelSize = 5; // Gaussian kernel size
    const float sigma = 1.0; // Standard deviation for Gaussian blur
    float kernel[kernelSize] = float[](0.06136, 0.24477, 0.38774, 0.24477, 0.06136); // Precomputed Gaussian kernel values
    vec2 offset[kernelSize] = vec2[](vec2(-2.0, 0.0), vec2(-1.0, 0.0), vec2(0.0, 0.0), vec2(1.0, 0.0), vec2(2.0, 0.0)); // Corresponding offset values

    for(int i = 0; i < kernelSize; ++i) {
        ivec2 samplePos = ivec2(subUv * vec2(radarSize)) + ivec2(offset[i].x, offset[i].y);
        ivec2 rotoPos = samplePos + ivec2(5, 20);
        uint rotoIndex = uint(rotoPos.y) * uint(ub.uniforms.GRAPHICS_MODE_WIDTH) + uint(rotoPos.x);
        Rotoscope rotoPixel = rotoPixels[rotoIndex];
        vec4 color = colortable[rotoPixel.EGAcolor & 0xf];
        radarPixel += color * kernel[i];
    }
    return radarPixel;
}

void main() {
    ivec2 pixelPos = ivec2(gl_GlobalInvocationID.xy);

    vec2 windowSize = vec2(float(ub.uniforms.WINDOW_WIDTH), float(ub.uniforms.WINDOW_HEIGHT));
    vec2 graphicSize = vec2(float(ub.uniforms.GRAPHICS_MODE_WIDTH), float(ub.uniforms.GRAPHICS_MODE_HEIGHT));
  
    vec2 uv = vec2(pixelPos) / windowSize;

    const float polygonWidth = ub.uniforms.WINDOW_WIDTH / ub.uniforms.GRAPHICS_MODE_WIDTH;

    // Calculate the corresponding position in the smaller texture
    uvec2 src = uvec2(floor(uv * graphicSize));

    // Calculate the index in the smaller texture

    uvec2 srcLookup = src + uvec2(4, 8);

    uint srcIndex = srcLookup.y * uint(ub.uniforms.GRAPHICS_MODE_WIDTH) + srcLookup.x;

    Rotoscope roto = rotoPixels[srcIndex];

    roto.blt_x = int(src.x) - 4;
    roto.blt_y = int(src.y) - 8;
    roto.blt_w = 72;
    roto.blt_h = 120;

    vec2 subUv = (uv * graphicSize) - vec2(src);

    // Pull the pixel from the smaller texture
    vec4 pixel = vec4(1.0, 0.0, 0.0, 1.0);

    // System and hyperspace
    pixel = DrawNavigationPixel(roto, uv, subUv);

    vec2 radarTL = vec2(0.02, 0.05);
    vec2 radarBR = vec2(0.25, 0.33);

    if(uv.x >= radarTL.x && uv.x <= radarBR.x && uv.y >= radarTL.y && uv.y <= radarBR.y) {
        vec2 normalizedUv = (uv - radarTL) / (radarBR - radarTL);
        pixel += DrawRadar(normalizedUv);
    }

    // Place the pixel in the larger surface
    imageStore(imgOutput, pixelPos, pixel);
}
